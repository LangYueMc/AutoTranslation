plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.matthewprenger.cursegradle"
    id "com.modrinth.minotaur"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-fabric:${rootProject.architectury_version}"
    modApi("me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    // ModMenu
    modApi("com.terraformersmc:modmenu:${rootProject.mod_menu_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
}
version = "${rootProject.mod_version}+mc${rootProject.minecraft_version}-fabric"

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": rootProject.mod_version
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set(null)
}

jar {
    archiveClassifier.set("dev")
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

task copy(type: Copy) {
    from "${project.buildDir}/libs"
    into "${rootProject.buildDir}/libs"
    dependsOn remapJar
}

build.finalizedBy(copy)

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

def CURSEFORGE_TOKEN = System.getenv("CURSEFORGE_TOKEN")
if (CURSEFORGE_TOKEN) {
    curseforge {
        apiKey = CURSEFORGE_TOKEN
        project {
            id = rootProject.curseforge_id
            releaseType = rootProject.version_type
            changelogType = 'markdown'
            changelog = rootProject.ext.v_changelog
            addGameVersion "Fabric"
            addGameVersion "Java 17"
            gameVersionStrings.addAll(rootProject.ext.gameVersionsList)
            mainArtifact remapJar
            relations {
                for (final def required in rootProject.ext.requiredProjectFabric) {
                    if (required.toString().trim().length() > 0)
                        requiredDependency required
                }
            }
        }
    }
    rootProject.tasks.getByName("curseforge").dependsOn tasks.getByName("curseforge")
}

def MODRINTH_TOKEN = System.getenv("MODRINTH_TOKEN")
if (MODRINTH_TOKEN) {
    modrinth {
        token = MODRINTH_TOKEN
        projectId = rootProject.modrinth_id
        versionNumber = rootProject.mod_version
        versionType = rootProject.version_type
        versionName = "${rootProject.archives_base_name} ${rootProject.mod_version} Fabric"
        uploadFile = remapJar
        getGameVersions().addAll(rootProject.ext.gameVersionsList)
        loaders = ['fabric']
        changelog = rootProject.ext.v_changelog
        dependencies {
            for (final def required_project in rootProject.ext.requiredProjectFabric) {
                if (required_project.toString().trim().length() > 0)
                    required.project required_project
            }
        }
    }
    rootProject.tasks.getByName("modrinth").dependsOn tasks.getByName("modrinth")
}
